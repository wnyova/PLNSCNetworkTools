buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath 'com.stanfy.spoon:spoon-gradle-plugin:1.2.2'
    // This is so we can publish straight to Google Play
    classpath 'com.github.triplet.gradle:play-publisher:1.2.2'
  }
}

apply plugin: 'com.android.application'
apply plugin: 'spoon'
apply plugin: 'com.github.triplet.play'

play {
  track = 'beta'
  if (project.hasProperty("ANDROID_NETWORK_TOOLS_SERVICE_ACCOUNT")) {
    serviceAccountEmail ANDROID_NETWORK_TOOLS_SERVICE_ACCOUNT
  }
  pk12File = file('../key.p12')
}

android {
  compileSdkVersion compileSdkVer
  buildToolsVersion buildToolsVer

  defaultConfig {
    applicationId 'com.yova.plnscnet'
    minSdkVersion minSdkVer
    targetSdkVersion targetSdkVer

    // When updating these, remember to update the vars in the root build.gradle
    versionName "0.4.5.3"
    versionCode 21

    archivesBaseName = "AndroidNetworkTools"
    testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
  }

  //check if the keystore details are defined in gradle.properties (this is so the key is not in github)
  if (project.hasProperty("ANDROID_NETWORK_TOOLS_STORE")) {
    signingConfigs {
      //from ~/.gradle/gradle.properties
      release {
        storeFile file(ANDROID_NETWORK_TOOLS_STORE)
        keyAlias ANDROID_NETWORK_TOOLS_ALIAS
        storePassword ANDROID_NETWORK_TOOLS_PASS
        keyPassword ANDROID_NETWORK_TOOLS_KEY_PASS
      }
    }
  }

  buildTypes {
    release {
      if (project.hasProperty("ANDROID_NETWORK_TOOLS_STORE")) {
        signingConfig signingConfigs.release
      }
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
    buildFeatures {
        viewBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

// Spoon used for CI / Testing
spoon {
  debug = true
  // To grant permissions to Android M >= devices */
  grantAllPermissions = true
}

dependencies {
  implementation 'androidx.appcompat:appcompat:1.6.1'
  implementation 'com.google.android.material:material:1.8.0'
  implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
  implementation 'de.hdodenhof:circleimageview:3.1.0'
  implementation project(':library')

  testImplementation 'junit:junit:4.12'

  androidTestImplementation 'com.squareup.spoon:spoon-client:1.6.4'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
  androidTestImplementation('androidx.test.espresso:espresso-intents:3.1.0')
}
